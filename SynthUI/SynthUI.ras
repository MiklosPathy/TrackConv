program SynthUI;

@use "SimpleCharFunctions"
@use "YM2151"
@use "X16Mouse"
@use "Input"

@define WidgetMapSize 4800
@define WidgetMaxCountByType 64

var
    WidgetMap: array[@WidgetMapSize] of byte;
    wmp:pointer;

	transpose:byte=3;

	mouseXchpos: byte;
	mouseYchpos: byte;
	
	slres:byte;
	
	mouseXchposAtClick: byte;
	mouseYchposAtClick: byte;
	mouseClicked:byte;
	
	tButton = record
		posX, posY, Width, Height, Color, Label : byte;
	end;

	buttons:array[@WidgetMaxCountByType] of tButton;
	
	tSlider = record
		posX, posY, Length_, Color, Value, Max_, Mode, AttachedLabel : byte;	
	end;

	@define Vertical 0
	@define Horizontal 1

	sliders:array[@WidgetMaxCountByType] of tSlider;

	clickedSlider:byte=$FF;

	tLabel = record
		posX, posY, Value, Color : byte;	
	end;

	labels:array[@WidgetMaxCountByType] of tSlider;

procedure putWidgetMapValue(posX,posY,value:byte);
var wmp:pointer;
	begin
	wmp:=WidgetMap[0];
	wmp:=wmp+(80*posY)+posX;
	wmp[0]:=value;
	end;

procedure getWidgetMapValue(posX,posY:byte);
var wmp:pointer;
	begin
	wmp:=WidgetMap[0];
	wmp:=wmp+(80*posY)+posX;
	ReturnValue(wmp[0]);
	end;

procedure ShowWidgetMap();
var x,y,v:byte;
	begin
	for y:=0 to 60 do
		for x:=0 to 80 do
			begin
			v:=getWidgetMapValue(x,y);
			SCF::DrawChar(x,y,v,1);
			end;
	end;

procedure ClearWidgetMap();
var i:integer;
	wmp,endpoint:pointer;
	begin
	wmp:=WidgetMap[0];
	endpoint:=wmp+@WidgetMapSize;
	while (wmp<endpoint) do
		begin
		wmp[0]:=$FF;
		inc(wmp);
		end;
	end;

procedure PlaceOnWidgetMap(posX,posY,Width,Height,WID:byte);
var x,y:byte;
	begin
	for y:=posY to posY+Height do
		for x:=posX to posX+Width do
			putWidgetMapValue(x,y,WID);
	end;

procedure drawLabel(lbl:byte);
	begin
	SCF::DrawHexByte(labels[lbl].posX,labels[lbl].posY,labels[lbl].Value,labels[lbl].Color);
	end;

procedure setLabelValue(lbl,value:byte);
	begin
	labels[lbl].Value:=value;
	drawLabel(lbl);
	end;

procedure handlebuttonclick();
	var clickedButton:byte;
	begin
	clickedButton:=getWidgetMapValue(mouseXchposAtClick,mouseYchposAtClick);
	if ((clickedButton>>6)=0) then
		begin
		case clickedButton of
		0: dec(transpose);
		1: inc(transpose);
		end;
		end;

	end;

procedure getSlidePosition(sllen,slval,slmax,slpos:byte);
	var temp:integer;
	begin
	if (sllen-1=slmax) then	slres:=slpos+slval
	else
		begin
		temp:=sllen*slval/slmax;
		slres:=temp+slpos;
		if slres>slpos+sllen-1 then slres:=slpos+sllen-1;
		end;
	end;

procedure getSliderValue(mousepos,sllen,slmax,slpos:byte);
	var temp,relpos:integer;
		rpb:byte;
	begin
	if (sllen-1=slmax) then	slres:=mousepos-slpos
	else
		begin
		temp:=0;
		rpb:=sllen/slmax/2;
		rpb:=mousepos-slpos+rpb;
		relpos:=rpb;
		temp:=relpos*slmax;
		temp:=temp/sllen;
		slres:=temp;	
		end;
	end;

procedure drawSlider(slid:byte);
	var i:byte;
	begin
	case sliders[slid].Mode of
		@Vertical:
			begin	
			for i:=sliders[slid].posY to sliders[slid].posY+sliders[slid].Length_ do
				SCF::DrawChar(sliders[slid].posX,i,$5D,sliders[slid].Color);

			getSlidePosition(sliders[slid].Length_,sliders[slid].Value,sliders[slid].Max_,sliders[slid].posY);
			SCF::DrawChar(sliders[slid].posX,slres,$5B,sliders[slid].Color);
			end;
		@Horizontal:
			begin
			for i:=sliders[slid].posX to sliders[slid].posX+sliders[slid].Length_ do
				SCF::DrawChar(i,sliders[slid].posY,$43,sliders[slid].Color);
			
			getSlidePosition(sliders[slid].Length_,sliders[slid].Value,sliders[slid].Max_,sliders[slid].posX);
			SCF::DrawChar(slres,sliders[slid].posY,$5B,sliders[slid].Color);
			end;
		end;
	if (sliders[slid].AttachedLabel<>$FF) then setLabelValue(sliders[slid].AttachedLabel,sliders[slid].Value);

	end;

procedure calcSliderpos(slid:byte);
	begin
	case sliders[slid].Mode of
		@Vertical:
			begin
			getSliderValue(mouseYchpos,sliders[slid].Length_,sliders[slid].Max_,sliders[slid].posY);	
			sliders[slid].Value:=slres;
			end;
		@Horizontal:
			begin	
			getSliderValue(mouseXchpos,sliders[slid].Length_,sliders[slid].Max_,sliders[slid].posX);	
			sliders[slid].Value:=slres;
			end;
		end;						
	end;

procedure handlesliderclick();
	var slID,WID,WT,my2,mx2:byte;
	begin
	if (mouseClicked=1 and clickedSlider<>$FF) then
		case sliders[clickedSlider].Mode of
			@Vertical:
				begin
				my2:=sliders[clickedSlider].posY+sliders[clickedSlider].Length_-1;
				if (mouseYchpos>=sliders[clickedSlider].posY and mouseYchpos<=my2) then 
					begin
					calcSliderpos(clickedSlider);
					drawSlider(clickedSlider);
					end;
				end;
			@Horizontal:
				begin
				mx2:=sliders[clickedSlider].posX+sliders[clickedSlider].Length_-1;
				if (mouseXchpos>=sliders[clickedSlider].posX and mouseXchpos<=mx2) then 
					begin
					calcSliderpos(clickedSlider);
					drawSlider(clickedSlider);
					end;
				end;
			end
	else	
	begin
	clickedSlider:=$FF;
	WID:=getWidgetMapValue(mouseXchpos,mouseYchpos);
	WT:=WID&%11000000;
	SCF::DrawBinByte(40,7,(WID),6);
	SCF::DrawBinByte(40,8,(WT),7);
	if WT=%01000000 then //Slider
		begin
		slID:=WID&%00111111;
		SCF::DrawHexByte(40,10,slID,1);
	
		clickedSlider:=slID;
		calcSliderpos(slID);
		drawSlider(slID);			
		end;
	end;

	SCF::DrawBinByte(40,6,clickedSlider,5);
		case clickedSlider of
			0:  begin
				slID:=sliders[0].Value;
				YM2151::SetFB($20,slID);
				end;
			1:	begin
				slID:=sliders[1].Value;
				end;
			end;

	end;

procedure handlemouse();
	begin
	X16Mouse::GetState();	
	
	SCF::DrawHexByte(75,56,X16Mouse::Buttons,1);
	SCF::DrawHexInt(70,57,X16Mouse::X,4);
	SCF::DrawHexInt(70,58,X16Mouse::Y,5);
	mouseXchpos:=X16Mouse::X>>3;
	mouseYchpos:=X16Mouse::Y>>3;
	SCF::DrawHexByte(75,57,mouseXchpos,4);
	SCF::DrawHexByte(75,58,mouseYchpos,5);
	SCF::DrawHexByte(75,59, getWidgetMapValue(mouseXchpos,mouseYchpos),7);

	if (X16Mouse::Buttons=1) then 
		begin
		handlesliderclick();
		if mouseClicked=0 then
			begin
			mouseXchposAtClick:=mouseXchpos;
			mouseYchposAtClick:=mouseYchpos;
			mouseClicked:=1;
			end;
		end;
	if (X16Mouse::Buttons=0) then 
		begin
			if mouseClicked=1 then
			begin
			handlebuttonclick();
			mouseClicked:=0;
			clickedSlider:=$FF;
			end;
		end;
	end;

procedure drawButton(butt:byte);
	var labelx,labely:byte;
	begin
	SCF::DrawBox(buttons[butt].posX,buttons[butt].posY,buttons[butt].Width,buttons[butt].Height,buttons[butt].Color);
	labelx:=buttons[butt].posX+buttons[butt].Width>>1;
	labely:=buttons[butt].posY+buttons[butt].Height>>1;
	SCF::DrawChar(labelx,labely,buttons[butt].Label,buttons[butt].Color);
	end;

procedure createButton(btID,posX,posY,Width,Height,Color,Label:byte);
var WID:byte;
	begin
	buttons[btID].posX:=posX;
	buttons[btID].posY:=posY;
	buttons[btID].Width:=Width;
	buttons[btID].Height:=Height;
	buttons[btID].Color:=Color;
	buttons[btID].Label:=Label;	
	WID:=btID&%00111111; //Bt type is 00;
	PlaceOnWidgetMap(posX,posY,Width,Height,WID);
	drawButton(btID);
	end;

procedure createSlider(slID,posX,posY,Length_,Color,Value,Max_,Mode,AttachedLabelID:byte);
var WID:byte;
	begin
	sliders[slID].posX:=posX;
	sliders[slID].posY:=posY;
	sliders[slID].Length_:=Length_;
	sliders[slID].Color:=Color;
	sliders[slID].Value:=Value;
	sliders[slID].Max_:=Max_;
	sliders[slID].Mode:=Mode;	
	sliders[slID].AttachedLabel:=AttachedLabelID;

	drawSlider(slID);

	WID:=slID|%01000000;
	case Mode of
		@Horizontal:PlaceOnWidgetMap(posX,posY,Length_,1,WID);
		@Vertical:PlaceOnWidgetMap(posX,posY,1,Length_,WID);
		end;

	end;
	
procedure createLabel(lbID,posX,posY,Value,Color:byte);
	begin
	labels[lbID].posX:=posX;
	labels[lbID].posY:=posY;
	labels[lbID].Value:=Value;
	labels[lbID].Color:=Color;
	drawLabel(lbID);
	//Label not Widget. No mouseclick. No Widgetmap
	end;

procedure createButtons();
	begin
	createButton(0,10,10,3,3,8,45);
	createButton(1,15,10,3,3,9,43);
	end;


procedure createSliders();
	begin
	SCF::TextAtPos(10,18,"FEEDBACK",4);		
	createLabel(0,10,29,0,4);
	createSlider(0,10,20,8,4,0,7,@Vertical,0);
	
	createLabel(1,6,40,0,5);
	createSlider(1,10,40,10,5,1,2,@Horizontal,1);

	createLabel(2,6,45,0,6);
	createSlider(2,10,45,10,6,1,9,@Horizontal,2);

	createLabel(3,6,50,0,7);
	createSlider(3,10,50,10,7,0,30,@Horizontal,3);
	end;


//MAIN START

begin
YM2151::Reset();
X16Mouse::CursorOn();

SCF::InitScreen(0);
SCF::TextAtPos(0,0,"YM2151 SYNTH UI",1);

ClearWidgetMap();

createButtons();
createSliders();

//ShowWidgetMap();
//return();

YM2151::SetReg($20,%11000111);	//RLFFFCCC		    Right Left Feedback Connect
YM2151::SetReg($60,21);			//0TTTTTTT			Total level (reversed 0 max, 127 min)
YM2151::SetReg($80,31);			//KK0AAAAA			KS    		AR			(Key Scaling, Attack)
YM2151::SetReg($A0,0);			//A00DDDDD			AMS-EN      D1R			(Amplitude modulation, Decay1)
YM2151::SetReg($C0,0);			//TT0DDDDD			DT2    		D2R 		(Useless Detune, Decay2
YM2151::SetReg($E0,5);			//DDDDRRRR			D1L			RR			(Sustain, Release)

	
Input::key:=$0;	
while (Input::key<>3) do
	begin
	Input::getin();
	if (Input::key<>0) then 
		begin	
		if (Input::key=$20) then YM2151::KeyOff() //Keyoff simulation with space
		else
			begin
			SCF::DrawHexByte(75, 55, Input::key, 5);
			Input::key2note();
			if(Input::note<>$FF) then
				begin
				YM2151::SetNote(Input::octave+transpose,Input::note); //Transpose
				YM2151::KeyOn();
				end;
			end;
		end;

	handlemouse();

	SCF::DrawHexByte(2,59,transpose,7);
	end;

X16Mouse::CursorOff();
YM2151::Reset();
return();
end.
 